package utbm.ia54.ant2dgrid.gui

import utbm.ia54.ant2dgrid.gui.fx.FxViewerController
import javafx.fxml.FXML
import javafx.scene.control.Button
import javafx.scene.layout.GridPane
import utbm.ia54.ant2dgrid.objects.Cell
import java.util.List
import utbm.ia54.ant2dgrid.objects.MapGenerator
import javafx.scene.control.TextField
import javafx.scene.input.MouseEvent
import javafx.^event.EventHandler
import utbm.ia54.ant2dgrid.events.ConfigureSimulation
import utbm.ia54.ant2dgrid.Enum.CellState
import utbm.ia54.ant2dgrid.gui.fx.AppExit
import utbm.ia54.ant2dgrid.events.EndAgent

/** 
 * @author aelez
 * @author Michel
 */

class Ant2DGridFxViewerController extends FxViewerController{

	@FXML
	private var spawnButton : Button
	
	@FXML
	private var gridZone : GridPane
	
	@FXML
	private var numberOfAnt : TextField

	@FXML
	private var btnMap : Button
	
	@FXML
	private var restartBtn : Button
	
	/*The grid a list of cell */
	private var grid : List<Cell>
	
	/*mapCreated a boolean */
	private var mapCreated = false
	
	/*launched a boolean */
	private var launched = false
	
	/*
	 * Gets the grid
	 * @return grid the list of cell
	 */
	def getGrid : List<Cell>{
		this.grid
	}
	
	/*
	 * Gets the quantity of ant
	 * @return quantity
	 */
	def getAntQuantity : int {
		Integer.parseInt(numberOfAnt.text)
	}
	
	/*
	 * Gets the number of rows
	 * @return width
	 */
	def getWidth : int{
		gridZone.rowConstraints.size
	}
	
	/*
	 * Gets the number of cols
	 * @return height
	 * 
	 */
	def getHeight : int{
		gridZone.columnConstraints.size
	}
	
	/*
	 * Creates the map
	 */
	@FXML
	def createMap {
		var generator = new MapGenerator(getWidth, getHeight)
		grid = generator.generateMap
		for (var i = 0; i < getWidth; i++) {
			for (var j = 0; j < getHeight; j++) {
				var cell = grid.get(i * getHeight + j)
				gridZone.add(cell, i, j)
				if (cell.state === CellState.NORMAL) {
					gridZone.add(cell.shapeAnt, cell.position.x, cell.position.y, 1, 1)
					gridZone.add(cell.pheromoneHome.objfx, cell.position.x, cell.position.y, 1, 2)
					gridZone.add(cell.pheromoneFood.objfx, cell.position.x, cell.position.y, 2, 2)
				}
				
				if(cell.state === CellState.FOOD || cell.state === CellState.HOME){
					gridZone.add(cell.food.shape, cell.position.x, cell.position.y, 2, 1)
				}
			}
		}
		if (mapCreated === false) {
			mapCreated = true
			btnMap.text = "Re-generate Map"
			spawnButton.disable = false
		}
		addCellEvent
	}

	/*
	 * Add event on the cell
	 */
	private def addCellEvent {
		gridZone.children.forEach[item | item.onMouseClicked = new EventHandler<MouseEvent>(){
			def handle(^event : MouseEvent) {
				if (item instanceof Cell) {
					var cell = item as Cell
					if (^event.clickCount === 1) {
						println(cell.toString)
					}
				}
			}
			
		}]
	}
	
	/*
	 * Launched the application
	 */
	@FXML
	def actionSpawn{
		var evt = new ConfigureSimulation
		if(!launched){
			startAgentApplication()[emitToAgent(evt)]
			launched = true
			btnMap.disable = true
			spawnButton.disable = true
			numberOfAnt.disable = true
			restartBtn.disable = false
		}else{
			emitToAgent(evt)
		}
	}
	
	/*
	 * Restart the application
	 */
	@FXML
	def onRestart{
		btnMap.disable = false
		numberOfAnt.disable = false
		restartBtn.disable = true
		launched = false
		spawnButton.disable = false
		emitToAgent(new EndAgent)
	}
}
